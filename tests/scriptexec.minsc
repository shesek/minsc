test("Script Exec", |T| {
  test("script::exec()", |T| {
    t::eq(script::exec(`1`), [ "success": true, "stack": [ 1 ] ]);
    t::eq(script::exec(`1 2`), [ "success": false, "stack": [ 1, 2 ] ]);
    t::eq(script::exec(`1 2 OP_ADD`), [ "success": true, "stack": [ 3 ] ]);
    t::eq(script::exec(`1 2 OP_EQUALVERIFY`), [ "success": false, "error": "EqualVerify", "failed_opcode": OP_EQUALVERIFY, "stack": [] ]);
    true
  });

  test("script::eval()", |T| {
    t::eq(script::eval(`1`), 1);
    t::eq(script::eval(`1 2`), [ 1, 2 ]);
    t::eq(script::eval(`1 2 OP_ADD`), 3);
    t::eq(script::eval(`1 OP_DROP`), []);
    true
  });

  test("script::verify()", |T| {
    test("Valid scripts", |T| {
      t::assert(script::verify(`1`));
      t::assert(script::verify(`2 2 OP_EQUAL`));
      t::assert(script::verify(`2 2 OP_EQUALVERIFY 999`));
      true
    });
    test("Invalid scripts", |T| {
      t::assert(!script::verify(`0`));
      t::assert(!script::verify(`1 2`));
      t::assert(!script::verify(`1 2 OP_EQUAL`));
      t::assert(!script::verify(`1 2 OP_EQUALVERIFY 1`));
      true
    });
    true
  });

  test("With stack", |T| {
    // just stack
    t::eq(script::eval(``, [ 1, 2 ]), [ 1, 2 ]);
    t::eq(script::eval(OP_ADD, [ 1, 2 ]), 3);
    t::eq(script::eval(`2 OP_ADD`, [ 1 ]), 3);
    // with options array
    t::eq(script::eval(OP_ADD, [ "stack": [ 1, 2 ] ]), 3);
    true
  });

  test("With ctx", |T| {
    // just ctx
    t::assert(script::verify(`9 OP_IF 1 OP_ENDIF`, ctx::legacy));
    t::eq(script::exec(`9 OP_IF OP_ENDIF`, ctx::tapscript)->error, "TapscriptMinimalIf");
    // with options array
    t::assert(script::verify(`9 OP_IF 1 OP_ENDIF`, [ "ctx": ctx::legacy ]));
    true
  });

  test("With stack & ctx", |T| {
    t::eq(script::eval(`9 OP_IF 8 OP_ENDIF`, [ 7 ], ctx::legacy), [ 7, 8 ]);
    t::eq(script::eval(`9 OP_IF 8 OP_ENDIF`, [ "stack": [ 7 ], "ctx": ctx::legacy ]), [ 7, 8 ]);
    true
  });

  test("script::trace()", |T| {
    t::eq(script::trace(`1 2 OP_ADD 0x102030 OP_DROP`), [
      "init_stack": [],
      "steps": [
        0: [ "opcode": OP_PUSHNUM_1, "stack": [ 1 ] ],
        1: [ "opcode": OP_PUSHNUM_2, "stack": [ 1, 2 ] ],
        2: [ "opcode": OP_ADD, "stack": [ 3 ] ],
        3: [ "opcode": OP_PUSHBYTES_3, "push": 0x102030, "stack": [ 3, 0x102030 ] ],
        7: [ "opcode": OP_DROP, "stack": [ 3 ] ]
      ],
      "result": [ "success": true, "stack": [ 3 ] ],
      "stats": [ "max_nb_stack_items": 2, "opcode_count": 0, "start_validation_weight": 51, "validation_weight": 51 ]
    ]);
    true
  });

  test("script::ptrace()", |T| {
    t::eq(script::ptrace(`1 2 OP_ADD 0x102030 OP_DROP`), [
      OP_PUSHNUM_1: [ 1 ],
      OP_PUSHNUM_2: [ 1, 2 ],
      OP_ADD: [ 3 ],
      OP_PUSHBYTES_3: 0x102030: [ 3, 0x102030 ],
      OP_DROP: [ 3 ],
      "success": true
    ]);
    t::eq(script::ptrace(`2 OP_ADD`, [ 1 ]), [
      "init_stack": [ 1 ],
      OP_PUSHNUM_2: [ 1, 2 ],
      OP_ADD: [ 3 ],
      "success": true,
    ]);
    true
  });

  test("TX", |T| {
    $sk = xpriv::rand();
    $script = `pubkey($sk) OP_CHECKSIGVERIFY 11 OP_EQUAL`;
    $utxo = wsh($script):1 BTC;

    $tx_unsigned = tx [
      "input": f45363eb3eb17863d3f0a9b2963df61fb8c775538def56bd801234dd7b1e8b49:1,
      "output": wpkh($sk/1):0.5 BTC,
    ];
    $sig = ecdsa::sign($sk, tx::sighash($tx_unsigned, 0, [ $utxo ])) + SIGHASH_ALL;

    test("script::exec() with tx", |T| {
      t::eq(script::exec($script, [
        "tx": $tx_unsigned,
        "ctx": ctx::segwitv0,
        "stack": [ 11, $sig ],
        "utxos": [ $utxo ]
      ]), [ "success": true, "stack": [1] ]);

      // Invalid
      t::assert(!script::verify($script, [ "tx": $tx_unsigned, "ctx": ctx::segwitv0, "stack": [ 12, $sig ], "utxos": [ $utxo ] ]));
      t::assert(!script::verify($script, [ "tx": $tx_unsigned, "ctx": ctx::segwitv0, "stack": [ 11, 0x010203 ], "utxos": [ $utxo ] ]));
      true
    });

    $tx_final = tx::with_witness($tx_unsigned, [
      0: [ 11, $sig, $script ]
    ]);

    test("tx::exec()", |T| {
      t::eq(tx::exec($tx_final, 0, [$utxo]), [ "success": true, "stack": [1] ])
    });

    test("tx::trace()", |T| {
      $trace = tx::trace($tx_final, 0, [$utxo]);
      t::assert($trace->result->success);
      t::eq(len($trace->steps), 4);
      t::eq($trace->steps.2, 35: [ "opcode": OP_PUSHNUM_11, "stack": [ 11, 11 ] ]);
      true
    });

    test("tx::verify()", |T| {
      t::assert(tx::verify($tx_final, 0, [$utxo]));

      t::assert(!tx::verify(tx::with_witness($tx_unsigned, [
        0: [ 12, $sig, $script ]
      ]), 0, [$utxo]));

      t::assert(!tx::verify(tx::with_witness($tx_unsigned, [
        0: [ 11, $sig+0x01, $script ]
      ]), 0, [$utxo]));
      true
    })
  })
})