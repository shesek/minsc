use std::str::FromStr;
use crate::time;
use crate::parser::{ast, concat, prepend, Expr, Stmt, ParseError};

grammar;

extern {
  type Error = ParseError;
}

match {
  // Skip extra whitespace
  r"\s*" => { },
  // Skip `//` comments
  r"//[^\n\r]*[\n\r]*" => { },
} else {
  // Takes priority over IDENT
  r"BTC|[mu]BTC|bits?|satoshis?|sats?|msats?" => BTC_DENOMINATION,
} else{
  r"-?\d{1,25}" => INT, // i64::MAX_NUMBER is 19 digits long
  r"-?\d{1,25}\.\d{1,25}" => FLOAT, // f64 provides precision for up to approx 17 decimal digits
  r"[a-zA-Z_$][a-zA-Z0-9_$]{0,38}(?:::[a-zA-Z0-9_$]+)*" => IDENT,
  r#""([^\\"]|\\[\\ntr"])*""# => STRING,
  r"0x[a-fA-F0-9]*" => BYTES,
  r"[a-fA-F0-9]{66}|[a-fA-F0-9]{64}|[a-fA-F0-9]{40}" => BYTES_NO0x, // allowed without the 0x prefix for exactly 33/32/20 bytes
  r"\d{4}-\d{1,2}-\d{1,2}T(\d{1,2}:\d{1,2}(:\d{1,2})?Z?)?" => DATETIME,
  r"[123][0-9a-zA-Z]{25,34}|((bc|tb|bcrt)1[0-9a-z]{38,60}|(BC|TB|BCRT)1[0-9A-Z]{38,60})" => ADDRESS,
  r"(\[[a-fA-F0-9]{8}(/\d+['h]?)*\])?[xt]pub[0-9a-zA-Z]{100,120}" => XPUB,
  r"\[[a-fA-F0-9]{8}(/\d+['h]?)*\]([a-fA-F0-9]{64}|[a-fA-F0-9]{66})" => SINGLE_PK_ORIG, // single pubkey with BIP32 origin
  _,
}

// Top-level program
// Can have a final return value or use the main() function instead
pub Program: Expr = <stmts:Stmt*> <ret:Return?> =>
  ast::Block { stmts, return_value: ret.map(Into::into) }.into();

// A collection of statements with no return value
// Used for library files and for the body of if statements
pub Stmts: ast::Stmts = <stmts:Stmt*> => ast::Stmts(stmts);

Return = {
  Expr, // the preferred form
  "return" <Expr> ";"?, // if you insist
};

// All possible statements
Stmt = {
  FnDef,
  Assign,
  CallStmt,
  IfStmt,
}

// All possible expressions. Includes SimpleExpr & SExpr
Expr = {
  SimpleExpr,
  And,
  Or,
  IfExpr,
  ChildDerive,
  FnExpr,
  Infix,
  Duration,
};

// A reduce set of expressions for use where Expr cannot be parsed unambiguously
// Includes SExpr, which can contain any wrapped expression
SimpleExpr = {
  SExpr,
  Not,
  Thresh,
  InfixProb,
  ArrayAccess,
};

// An even more reduced set of expressions for use where SimpleExpr cannot be parsed unambiguously
SExpr = {
  Number,
  Ident,
  Bytes,
  String,
  Call,
  Array,
  DateTime,
  BtcAmount,
  Address,
  PubKey,
  ScriptFrag,

  // Allow any expression wrapped in () or {}
  Paren<Expr>,
  BlockExpr,
};

//
// Expressions
//

Number = { Int, Float };

Int: Expr = INT =>? Ok(i64::from_str(<>).map_err(ParseError::from)?.into());
Float: Expr = FLOAT =>? Ok(f64::from_str(<>).map_err(ParseError::from)?.into());

IdentTerm: ast::Ident = IDENT => ast::Ident(<>.into());
Ident: Expr = IdentTerm => Expr::Ident(<>);

Call: Expr = Call_ => Expr::Call(<>);
CallLiteral: Expr = CallLiteral_ => Expr::Call(<>);

Call_: ast::Call = { CallLiteral_, CallExpr_, PipeCall_ };
CallLiteral_: ast::Call = <func:Ident> <args:Paren<List0<Expr, ",">>> => ast::Call { func: func.into(), args };
CallExpr_: ast::Call = <func:WrapExpr> <args:Paren<List0<Expr, ",">>> => ast::Call { func: func.into(), args };

// Pipe call: `$first_arg | func($extra_args...)` -> `func($first_arg, $extra_args...)`
PipeCall_: ast::Call = <first_arg:PipeCallLHS> "|" <func:PipeCallFunc> <extra_args:Paren<List0<Expr, ",">>> =>
  ast::Call { func: func.into(), args: prepend(extra_args, first_arg) };
PipeCallLHS = { SExpr, ArrayAccess, Duration };
PipeCallFunc = { Ident, WrapExpr };

WrapExpr = { Paren<Expr>, BlockExpr };

IfExpr: Expr = "if" <condition:Expr> <then_val:IfExprThenVal> "else" <else_val:Expr> =>
    ast::IfExpr { condition: condition.into(), then_val: then_val.into(), else_val: else_val.into() }.into();

IfExprThenVal = { BlockExpr, "then" <Expr> };

And: Expr = <List2<AndOrBranch, "&&">> => ast::And(<>).into();
Or: Expr = <List2<AndOrBranch, "||">> => ast::Or(<>).into();
AndOrBranch = { SimpleExpr, ChildDerive };

Thresh: Expr = <thresh:SExpr> "of" <policies:SimpleExpr> =>
  ast::Thresh { thresh: thresh.into(), policies: policies.into() }.into();

// A { .. } block expression with a final return value
BlockExpr: Expr = "{" <stmts:Stmt*> <ret:Return> "}" =>
  ast::Block { stmts, return_value: Some(ret.into()) }.into();

Array = { ArraySquare, ArrayCurly }

// Standard square brackets array - [E1, E2, E3, ...]
ArraySquare: Expr = "[" <List0<Expr, ",">> "]" =>
  ast::Array(<>).into();

// Support curly brackets arrays with exactly two elements: {E1,E2}
// For compatibility with the BIP386 tr() Output Descriptor syntax for script trees
ArrayCurly: Expr = "{" <e1:Expr> "," <e2:Expr> "}" =>
  ast::Array(vec![e1, e2]).into();

ArrayAccess_: Expr = <array:ArrayAccessLHS> "." <index:ArrayAccessRHS> =>
  ast::ArrayAccess { array: array.into(), index: index.into() }.into();
ArrayAccessLHS = { Ident, CallLiteral, Array, BlockExpr, Bytes, ArrayAccess };
ArrayAccessRHS = { Int, Ident, Paren<Expr>, BlockExpr };

// Hack :<
// $arr.0.1 gets tokenized with 0.1 as a single FLOAT token during the lexer stage.
// This fixes it by transforming the resulting $arr.(0.1) to ($arr.0).1
ArrayAccessFixFloat: Expr = <array:ArrayAccessLHS> "." <two_indices:FLOAT> =>? {
  let mut current = array;
  for index in two_indices.split(".") {
    let index: i64 = index.parse().map_err(ParseError::from)?;
    current = ast::ArrayAccess { array: current.into(), index: Expr::Int(index).into() }.into();
  }
  Ok(current)
};
ArrayAccess = { ArrayAccess_, ArrayAccessFixFloat };

// An xpub or standalone public key (33 bytes or 32 for x-only, uncompressed is unsupported)
PubKey: Expr = PubKey_ =>? Ok(Expr::PubKey(<>.parse().map_err(ParseError::from)?));
PubKey_ = { XPUB, SINGLE_PK_ORIG };

Bytes: Expr = {
  BYTES =>? Ok(Expr::bytes_from_hex(&<>[2..])?),

  // Bytes are typically constructed with the `0x` prefix, but can be constructed without it if
  // the length is exactly 33, 32 or 20 bytes. This is done for compatibility with the Miniscript
  // Policy syntax for literal pubkeys, x-only-pubkeys and hashes. The Bytes value will later get
  // get coerced into a PubKey/Hash as needed. Note that SINGLE_PK_ORIG already matches 0x-less
  // pubkeys that include bip32 origin info, and constructs them directly as PubKeys.
  BYTES_NO0x =>? Ok(Expr::bytes_from_hex(&<>)?),
};

// Addresses in Bech32 or Base58check encoding
// Supports the 1/2/3 prefixes for Base58 and bc/tb/bcr for Bech32 (mainnet, signet, testnet and regtest)
//
// Bech32 addresses are only supported up to 63 characters. This is OK for Segwit v0/v1 (always 42/62),
// but may not be sufficient for future versions which can be up to 74 characters long.
// Supporting them requires resolving the ambiguity with 64-characters long hexadecimal strings.
Address: Expr = ADDRESS =>? Ok(Expr::Address(<>.parse().map_err(ParseError::from)?));

String: Expr = STRING => Expr::string_from_escaped_str(&<>[1..<>.len()-1]);

// BIP32 child key derivation
ChildDerive: Expr = {
    <parent:SimpleExpr> "/" <path:List1<ChildDerivePart, "/">> <wildcard:ChildDeriveWildcard?> =>
      ast::ChildDerive { parent: parent.into(), path, is_wildcard: wildcard.is_some() }.into(),
    <parent:SimpleExpr> <wildcard:ChildDeriveWildcard> =>
      ast::ChildDerive { parent: parent.into(), path: vec![], is_wildcard: true }.into(),
};
ChildDerivePart: Expr = {
  SimpleExpr,
  // For compatibility with the BIP389 multipath descriptor syntax: XPUB/0/<0;1>/9
  // This can alternatively be expressed using standard Minsc arrays: XPUB/0/[0,1]/9
  "<" <List2<SimpleExpr, ";">> ">" => ast::Array(<>).into(),
};
// Allow `xpub/ *` so that it doesn't look like a comment.
ChildDeriveWildcard = { "/*", "/ *" };

ScriptFrag: Expr = "`" <fragments:ScriptFragPart*> "`" => ast::ScriptFrag { fragments }.into();
ScriptFragPart = { ScriptFragPart_, ScriptFragPart_Infix };
ScriptFragPart_ = {
  Ident, Number, String, Bytes, PubKey, DateTime, CallLiteral, ArrayAccess, Array,
  ScriptFragPart_Mark, BlockExpr, "<" <SimpleExpr> ">", "<" <Duration> ">",
};
// Cannot allow all Infix operators because < > conflicts
ScriptFragPart_Infix: Expr = <lhs:ScriptFragPart_> <op:InfixOpScriptSafe> <rhs:ScriptFragPart_RHS> =>
   ast::Infix { op, lhs: lhs.into(), rhs: rhs.into() }.into();

ScriptFragPart_RHS = { SExpr, ArrayAccess };

// Script Marker syntax, only valid within script fragments.
// Translated into the AST as a Call, it probably shouldn't. Just wanted to hack something together quickly. :)
ScriptFragPart_Mark = { ScriptFragPart_MarkIdent, ScriptFragPart_MarkExpr, ScriptFragPart_Label, ScriptFragPart_Comment, ScriptFragPart_Context };

ScriptFragPart_MarkExpr: Expr = "@" <Paren<(<Expr> "," <Expr>)>> =>
  ast::Call { func: Expr::Ident("mark".into()).into(), args: vec![<>.0, <>.1] }.into();
ScriptFragPart_MarkIdent: Expr = "@" <kind:IDENT> <body:Paren<Expr?>> => {
  let body = body.unwrap_or_else(|| Expr::String("".to_string()));
  ast::Call { func: Expr::Ident("mark".into()).into(), args: vec![Expr::String(kind.to_string()), body] }.into()
};
ScriptFragPart_Label: Expr = "@" <IDENT> =>
  ast::Call { func: Expr::Ident("mark::label".into()).into(), args: vec![Expr::String(<>.to_string())] }.into();
ScriptFragPart_Comment: Expr = "#" <ScriptFragPart_RHS> =>
  ast::Call { func: Expr::Ident("mark::comment".into()).into(), args: vec![<>] }.into();

ScriptFragPart_Context: Expr = "@@" <Paren<(<Expr> "," <Expr>)>> =>
  ast::Call {
    func: Expr::Ident("mark::ctx".into()).into(),
    // Wrap the body expression in a function, to be evaluated from within the `mark::ctx()` scope.
    args: vec![<>.0, ast::FnExpr { signature: vec![], body: <>.1.into() }.into()],
  }.into();


// Anonymous function expression
FnExpr: Expr = <signature:FnExprSignature> <body:Expr> =>
    ast::FnExpr { signature, body: body.into() }.into();
FnExprSignature = {
  "|" <List0<IdentTerm, ",">> "|",
  // No arguments should already be captured by the `List0`, but for some reason it isn't.
  // Possibly because `||` is also a terminal for policy OR?
  "||" => vec![],
};

// Infix op with two operands
Infix: Expr = <lhs:InfixLhs> <op:InfixOp> <rhs:Expr> =>
   ast::Infix { op, lhs: lhs.into(), rhs: rhs.into() }.into();

InfixLhs = { SimpleExpr, Duration, ChildDerive };

InfixOp: ast::InfixOp = { InfixOpScriptSafe, InfixOpNonScript };

// Can be used everywhere
InfixOpScriptSafe: ast::InfixOp = {
  "+" => ast::InfixOp::Add,
  "-" => ast::InfixOp::Subtract,
  "*" => ast::InfixOp::Multiply,
  "==" => ast::InfixOp::Eq,
  "!=" => ast::InfixOp::NotEq,
  ":" => ast::InfixOp::Colon,
};
// Cannot be used within a ScriptFrag
InfixOpNonScript: ast::InfixOp = {
  // > and < conflict with the <$data> syntax
  ">" => ast::InfixOp::Gt,
  "<" => ast::InfixOp::Lt,
  // could be allowed in ScriptFrag but seems weird to
  ">=" => ast::InfixOp::Gte,
  "<=" => ast::InfixOp::Lte,
};


// This is defined separately from InfixOp with less options for the LHS/RHS, so that
// it can be used unambiguously as an AndOrBranch element without requiring parenthesis.
InfixProb: Expr = <lhs:SExpr> "@" <rhs:SimpleExpr> =>
  ast::Infix { op: ast::InfixOp::Prob, lhs: lhs.into(), rhs: rhs.into() }.into();

Not: Expr = "!" <SimpleExpr> =>
  ast::Not(<>.into()).into();

// Duration and times

Duration = { DurationBlocks, DurationTime };

DurationBlocks: Expr = <num:SExpr> "blocks" =>
  ast::Duration::BlockHeight(num.into()).into();

DurationTime: Expr = <heightwise:"heightwise"?> <parts:DurationTimePart+>  =>
  ast::Duration::BlockTime { parts, heightwise: heightwise.is_some() }.into();

DurationTimePart: ast::DurationPart = <n:DurationTimeNum> <i:DurationTimeUnit> => (n.into(), i);
DurationTimeNum = { Number, CallLiteral, Ident, Paren<Expr> };
DurationTimeUnit: ast::DurationUnit = {
  "year" => ast::DurationUnit::Years,
  "years" => ast::DurationUnit::Years,
  "month" => ast::DurationUnit::Months,
  "months" => ast::DurationUnit::Months,
  "week" => ast::DurationUnit::Weeks,
  "weeks" => ast::DurationUnit::Weeks,
  "day" => ast::DurationUnit::Days,
  "days" => ast::DurationUnit::Days,
  "hour" => ast::DurationUnit::Hours,
  "hours" => ast::DurationUnit::Hours,
  "minute" => ast::DurationUnit::Minutes,
  "minutes" => ast::DurationUnit::Minutes,
  "second" => ast::DurationUnit::Seconds,
  "seconds" => ast::DurationUnit::Seconds,
}

DateTime: Expr = DATETIME =>? Ok(Expr::DateTime(time::parse_datetime(<>)?));

BtcAmount: Expr = <num:SExpr> <denom:BTC_DENOMINATION> =>
  ast::BtcAmount(num.into(), denom.parse().expect("must be valid")).into();

//
// Statements
//

Assign: Stmt = "let"? <assigns:List1<Assignment, ",">> ";" =>
    ast::Assign(assigns).into();

Assignment: ast::Assignment = <lhs:IdentTerm> "=" <rhs:Expr> =>
    ast::Assignment { lhs, rhs };

FnDef: Stmt = {
    "fn" <ident:IdentTerm> <signature:Paren<List0<IdentTerm, ",">>> "=" <body:Expr> ";" =>
        ast::FnDef { ident, signature, body }.into(),
    "fn" <ident:IdentTerm> <signature:Paren<List0<IdentTerm, ",">>> <body:BlockExpr> ";"? =>
        ast::FnDef { ident, signature, body }.into(),
}

CallStmt: Stmt = <Call_> ";" => ast::CallStmt(<>).into();

IfStmt: Stmt = "if" <condition:Expr> "{" <then_body:Stmts> "}" <else_body:IfStmtElse?> =>
    ast::IfStmt { condition: condition.into(), then_body: then_body.into(), else_body: else_body.into() }.into();
IfStmtElse: ast::Stmts = {
  "else" "{" <Stmts> "}",
  // allow nested `else if` statements without `{}`
  "else" <IfStmt> => ast::Stmts(vec![<>]),
};

// Helpers

// A `S`-separated list of zero or more `T` values
List0<T, S>: Vec<T> = <l:(<T> S)*> <t:T?> => concat(l, t);

// A `S`-separated list of one or more `T` values
List1<T, S>: Vec<T> = <l:(<T> S)*> <t:T> => concat(l, Some(t));

// A `S`-separated list of two or more `T` values
List2<T, S>: Vec<T> = <l:(<T> S)+> <t:T> => concat(l, Some(t));

Paren<T> = "(" <T> ")";
