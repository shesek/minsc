// Opcode aliases
OP_TRUE = `1`, OP_FALSE = `0`, OP_0 = OP_FALSE;
OP_CHECKSEQUENCEVERIFY = OP_CSV;

// Sighash types, including combinations with ANYONECANPAY
SIGHASH_DEFAULT=0x00, SIGHASH_ALL=0x01, SIGHASH_NONE=0x02, SIGHASH_SINGLE=0x03,
SIGHASH_ALL_ANYONECANPAY=0x81, SIGHASH_NONE_ANYONECANPAY=0x82, SIGHASH_SINGLE_ANYONECANPAY=0x83;

// Dust amounts
DUST_WPKH=294, DUST_WSH=330, DUST_TR=330, DUST_P2A=240, DUST_PRE_SW=546,
DUST_AMOUNT=DUST_WSH; // also == DUST_TR

// Pay-to-anchor witness program
P2A = `1 0x4e73`;

// A point with unknown discrete logarithm (to eliminate key-path spend)
// Uses the sha256 of G in DER encoding, as suggested by BIP 341: https://github.com/bitcoin/bips/blob/master/bip-0341.mediawiki#constructing-and-spending-taproot-outputs
// assert::eq(NUMS, pubkey(hash::sha256(0x0479be667ef9dcbbac55a06295ce870b07029bfcdb2dce28d959f2815b16f81798483ada7726a3c4655da4fbfc0e1108a8fd17b448a68554199c47d08ffb10d4b8)));
NUMS = pubkey(0x50929b74c1a04954b78b4b6035e97a5e078a5a0f28ec96d547bfee9ace803ac0);

// A unique NUMS as a derivable Xpub, using the SHA256 of $seed as the chain code
// version(4) + depth(1) + fingerprint(4) + childnum(4) + chaincode(32) + pubkey_with_parity(1+32)
fn xNUMS($seed, $ver=BIP32_MAIN) = pubkey($ver + 0x000000000000000000 + hash::sha256($seed) + 0x02 + bytes(NUMS))/0;

// BIP32 version magic bytes
BIP32_MAIN = 0x0488b21e;
BIP32_TEST = 0x043587cf;

// Useful as a default with CTV
PREVOUT_NULL = 0000000000000000000000000000000000000000000000000000000000000000:4294967295;

// Enables RBF and absolute lock time, disables relative lock time
ENABLE_RBF = 0xFFFFFFFD;

//
// Taproot
//

// The default unspendable key for tr(), can be overridden in user-land code
TR_UNSPENDABLE = NUMS;

TR_ANNEX_BYTE = 0x50;

// Create the witness stack for a Taproot script-path spend
fn tr::script_witness($tr, $script_or_policy, $stack=[], $annex=null) {
  $script = if typeof($script_or_policy) == "policy" then tapscript($script_or_policy) else $script_or_policy;
  $stack + [ $script, tr::ctrl($tr, $script) ]
  + (if $annex != null then [ TR_ANNEX_BYTE + $annex ] else [])
}

//
// Transactions
//

// Simplified SigHash API given a Transaction and its UTXOs
fn tx::sighash($tx, $vin, $utxos, $sighash_ty=SIGHASH_ALL, $tr_script_leaf=null) {
  $psbt = psbt[
    "unsigned_tx": tx::strip_witness($tx),
    "utxos": $utxos,
    "inputs": [ $vin: [ "sighash_type": $sighash_ty ] ],
  ];
  if $tr_script_leaf == null then psbt::sighash($psbt, $vin)
  else psbt::sighash($psbt, $vin, $tr_script_leaf)
}
// XXX support segwit v0 with only the spent utxo & pre-segwit with only the spent utxo's scriptPubKey

fn tx::sign($tx, $utxos, $sk, $inputs=[]) = psbt::sign_extract([
  "unsigned_tx": $tx,
  "utxos": $utxos,
  "inputs": $inputs, // optionally set additional psbt input fields (typically populated automatically from the utxo)
], $sk);
// XXX return which inputs was signed?

fn tx::fee($tx, $utxos) = psbt::fee[
  "unsigned_tx": tx::strip_witness($tx),
  "utxos": $utxos,
];

fn tx::strip_witness($tx) = tx::with_witness($tx, fillArray(len(tx($tx)->input), |$vin| $vin: []));

//
// PSBT
//

fn psbt::finalize_witness($psbt, $input_witnesses, $extract_tx=true) {
  $finalized = psbt::update($psbt, [
    "inputs": map($input_witnesses, |[ $input_index, $witness ]|
      $input_index: [ "final_script_witness": $witness ]
    )
  ]);
  if $extract_tx then psbt::extract_raw($finalized) else $finalized
}

fn psbt::finalize_extract($psbt) = psbt::extract(psbt::finalize($psbt));
fn psbt::sign_extract($psbt, $keys) = psbt::extract(psbt::finalize(psbt::sign($psbt, $keys)));

//
// Script utilities
//

OP::NOT_EQ_VERIFY = `OP_EQUAL OP_NOT OP_VERIFY`;

// ROLL/PICK from the bottom of the stack (1 for the bottom-most stack element - NOT 0)
// stack in: <index>, stack out: <element>
OP::PICK_BOTTOM = `OP_TOALTSTACK OP_DEPTH OP_FROMALTSTACK OP_SUB OP_PICK`;
OP::ROLL_BOTTOM = `OP_TOALTSTACK OP_DEPTH OP_FROMALTSTACK OP_SUB OP_ROLL`;

// ROLL/PICK from the bottom of the stack, for static $n (1 for bottom-most)
fn rollBottom($n) = `OP_DEPTH {if $n==1 then OP_1SUB else `$n OP_SUB`} OP_ROLL`;
fn pickBottom($n) = `OP_DEPTH {if $n==1 then OP_1SUB else `$n OP_SUB`} OP_PICK`;

OP::SHIFT = rollBottom(1);

// Verify that the top stack element is exactly 1 or 0
OP::MINIMAL_BOOL = `OP_DUP OP_SIZE OP_EQUALVERIFY`;

// Sort the top 2 stack elements (higher placed at the top)
OP::2SORT = `OP_2DUP OP_GREATERTHAN OP_IF OP_SWAP OP_ENDIF`;

// Left-inclusive only range check
fn verifyWithin($min, $max) = `OP_DUP $min $max OP_WITHIN OP_VERIFY`;
// Right-inclusive too
fn verifyRange($min, $max) = verifyWithin($min, $max+1);

// Drop $num elements from the stack/altstack
fn dropStack($num) = `OP_2DROP*($num/2) OP_DROP*($num%2)`;
fn dropAlt($num) = `OP_FROMALTSTACK*$num dropStack($num)`;

// MUL for small constant numbers
OP::2MUL = `OP_DUP OP_ADD`;
OP::3MUL = `OP_DUP OP::2MUL OP_ADD`;
OP::4MUL = OP::2MUL*2;
OP::5MUL = `OP_DUP OP::4MUL OP_ADD`;
OP::6MUL = `OP::3MUL OP::2MUL`;
OP::7MUL = `OP_DUP OP::6MUL OP_ADD`;
OP::8MUL = `OP::4MUL OP::2MUL`;
OP::9MUL = OP::3MUL*2;

//
// Script Looping utilities
//

// Loop unrolling: run $loop_body as long as $condition is met, up to $max_iterations times.
// $loop_body can be a Script or a Function that accepts the iteration count and returns a Script.
// For example: `<num> unrollLoop(50, `OP_DUP 0 OP_GREATERTHANOREQUAL`, `OP_1SUB`)` to count down from <num> to 0, for <num>s of up to 50
// more advanced example with some comments: https://min.sc/v1/#gist=758c25489869d77d4ef624ea43f18c49
fn unrollLoop($max_iterations, $condition, $loop_body) {
  $body_fn = if isFunction($loop_body) then $loop_body else |_| $loop_body;

  fn _unrollLoop($i) =
    if $i < $max_iterations then `$condition OP_IF $body_fn($i) _unrollLoop($i+1) OP_ENDIF`
    else `$condition OP_NOT OP_VERIFY` // fail the script if the condition is still met after $max_iterations
  ;
  _unrollLoop(0)
}

// Read the top stack item as the number of times to run $for_body
// Example with accumulator: `0 <num> unrollFor(50, `OP_DUP OP_ROT OP_ADD OP_SWAP`)` to sum the numbers from <num> to 1
fn unrollFor($max_iterations, $for_body) {
  $body_fn = if isFunction($for_body) then $for_body else |_| $for_body;
  `
    unrollLoop($max_iterations, `OP_DUP OP_0NOTEQUAL`, |$i| `$body_fn($i) OP_1SUB`)
    OP_DROP // drop the counter (always 0 by now)
  `
}

//
// Script Altstack manipulation
//

// ROLL/PICK from the altstack, for static $n
fn rollAlt($n) = `
  OP_FROMALTSTACK*$n
  {`OP_SWAP OP_TOALTSTACK`*($n-1)}
`;
fn pickAlt($n) = `
  OP_FROMALTSTACK*$n
  OP_DUP OP_TOALTSTACK
  {`OP_SWAP OP_TOALTSTACK`*($n-1)}
`;

// ROLL/PICK from the altstack, using the number at the top of the stack as the depth
fn nRollAlt($max_n) = `
  OP_DUP
  unrollFor($max_n, `OP_FROMALTSTACK OP_ROT*2`)
  OP_1SUB
  unrollFor($max_n, `OP_ROT OP_TOALTSTACK`)
`;
fn nPickAlt($max_n) = `
  OP_DUP
  unrollFor($max_n, `OP_FROMALTSTACK OP_ROT*2`)
  OP_SWAP OP_DUP OP_ROT
  unrollFor($max_n, `OP_ROT OP_TOALTSTACK`)
`;

// Read the top stack item as the number of stack items to send to the altstack
// stack in: <el1> <el2> .. <elN> <N total>, stack out: (empty)
// altstack out: <elN> .. <el2> <el1> <N total>
fn nToAlt($max_n) = `
  OP_DUP // keep a copy of the total number of elements
  // stack: <el1> <el2> .. <elN> <N total> <N counter>
  unrollFor($max_n, `OP_ROT OP_TOALTSTACK`)
  OP_TOALTSTACK // send the total number of elements as the final altstack item
`;

// The reverse of nToAlt() - bring back items from the altstack to the stack
// altstack in: <elN> .. <el2> <el1> <N total>, altstack out: (empty)
// stack in: (none), stack out: <el1> <el2> .. <elN> <N total>
fn nFromAlt($max_n) = `
  OP_FROMALTSTACK
  OP_DUP // keep a copy of the total number of elements
  // stack: <N total> <N counter>, alt stack: <el1> <el2> .. <elN>
  unrollFor($max_n, `OP_FROMALTSTACK OP_ROT*2`)
`;

//
// Script Conditional control structures
//

// Check a series of nested OP_IF..OP_ELSE conditions, running the script associated with the first
// match or the optional `default` branch. If there's no match and no `default`, execution fails.
//
// For example, to match a number on the stack and run some code accordingly:
// ifelseif [
//   `OP_DUP 0 OP_EQUAL`: `OP_DROP $alice_pk OP_CHECKSIG`,
//   `OP_DUP 1 OP_EQUAL`: `OP_DROP <6 months> OP_CSV OP_DROP`,
//   default:             `OP_DROP $bob_pk OP_CHECKSIG`
// ]
fn ifelseif($clauses) = if isEmpty($clauses) then `` else `
  // Check all clauses but the last, running the first one that matches
  map(initial($clauses), |[ $condition, $body ]| `$condition OP_IF $body OP_ELSE`)

  // The last clause is checked using OP_VERIFY instead of another IF..ELSE to save some bytes.
  { if last($clauses).0 != default then `last($clauses).0 OP_VERIFY` else ``}
  last($clauses).1

  OP_ENDIF*(len($clauses) - 1)
`;

// Match the item at the top of the stack against the clauses and run the matching script,
// keeping the item being matched around as necessary. For example:
// match [
//   `0 OP_EQUAL`: `$alice_pk OP_CHECKSIG`,
//   `1 OP_EQUAL`: `<6 months> OP_CSV OP_DROP`,
// ]
fn match($clauses) = if isEmpty($clauses) then `` else
  ifelseif(
    // DUP the value being matched to keep it available for the next clause, while allowing condition
    // scripts to consume it (with e.g. OP_EQUAL). It will be removed before the matching script is run.
    map(initial($clauses), |[ $condition, $body ]| `OP_DUP $condition`: `OP_DROP $body`)

    // The last clause doesn't need to DUP/DROP as there are no more conditions that need the value.
    // In the case of a `default` branch, the value will remain available on stack.
    + [ last($clauses) ]
  );

// Match the item at the top of the stack for equality.
// For example: switch[ 0: `$alice_pk OP_CHECKSIG`, 1: `<6 months> OP_CSV OP_DROP` ]
fn switch($clauses) = match(map($clauses, |[ $condition, $body ]| {
  $condition = if $condition != default then `$condition OP_EQUAL` else default;
  $condition: $body
}));

// Pop an index number off the stack and execute the `$scripts` branch with that index.
// This can typically be better accomplished with separate Taproot leaves for the script
// branches, but branching within a single Script may be preferable for some simple cases.
// Does not support a `default` branch.
// For example: select[ `$alice_pk OP_CHECKSIG`, `<6 months> OP_CSV OP_DROP` ]
fn select($scripts) =
  // special-case optimization for 2 branches (MINIMALIF enforces 0/1 only)
  if len($scripts) == 2 then `OP_IF $scripts.1 OP_ELSE $scripts.0 OP_ENDIF`
  else switch(enumerated($scripts))
;

//
// Script Marker utilities
//

MARK_SCRIPT=true;
MARK_CTX=[];
// SCRIPT_MARKER_MAGIC available from Rust

dyn fn mark($kind, $body) = if !MARK_SCRIPT then `` else
  `SCRIPT_MARKER_MAGIC OP_NOTIF $kind str($body) OP_ENDIF`;
dyn fn mark::comment($comment) = mark("comment", join(MARK_CTX+[str($comment)], " Â· "));
dyn fn mark::label($name) = mark("label", join(MARK_CTX+[str($name)], "__"));
dyn fn mark::ctx($context, $fn) = `
  #("BEGIN "+$context)
  $fn(MARK_CTX + [ str($context) ])
  #("END "+$context)
`;


//
// Dummy key generation
//

// Single keys
fn dummy::sk($seed=0) = seckey(rand::bytes(32, $seed));
fn dummy::pk($seed=0) = pubkey(dummy::sk($seed));
fn dummy::kpair($seed=0) { $sk=dummy::sk($seed); pubkey($sk):$sk }
fn dummy::sks($num, $seed=0) = fillArray($num, |$i| dummy::sk($seed+$i));
fn dummy::pks($num, $seed=0) = map(dummy::sks($num, $seed), pubkey);
fn dummy::kpairs($num, $seed=0) = flatten(fillArray($num, |$i| dummy::kpair($seed+$i)));

// BIP 32 Xpubs/Xprivs
fn dummy::xpriv($seed=0) = xpriv::from_seed(rand::bytes(32, $seed), _$$_RECKLESSLY_RISK_MY_BITCOINS_$$_); // use the more familiar mainnet 'xpub'/'xprv' prefix for examples
fn dummy::xpub($seed=0) = pubkey(dummy::xpriv($seed));
fn dummy::xpair($seed=0) { $sk=dummy::xpriv($seed); pubkey($sk):$sk }
fn dummy::xprivs($num, $seed=0) = fillArray($num, |$i| dummy::xpriv($seed+$i));
fn dummy::xpubs($num, $seed=0) = map(dummy::xprivs($num, $seed), pubkey);
fn dummy::xpairs($num, $seed=0) = flatten(fillArray($num, |$i| dummy::xpair($seed+$i)));
